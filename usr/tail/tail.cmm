/*
 * TacOS Source Code
 *    Tokuyama kousen Advanced educational Computer.
 *
 * Copyright (C) 2021 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 */

/*
 * tail/tail.cmm : tail プログラム
 *
 * 2021.01.12 : 新規作成
 *
 */

#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>

void usageExit(char[] argv0) {
    fprintf(stderr, "Usage: %s [-n count] <fileName>...\n", argv0);
    exit(1);
}

public int main(int argc, char[][] argv) {
  char[] fname;
  int count = 11;
  if (argc==4) {
    if (strCmp("-n", argv[1])!=0) usageExit(argv[0]);
    count = atoi(argv[2])+1;
    fname = argv[3];
  } else if (argc==2) {
    fname = argv[1];
  } else {
    usageExit(argv[0]);
  }

  FILE fp = fopen(fname, "r");
  if (fp==null) {
    perror(fname);
    return 1;
  }

  int[] pos = iMalloc(count);
  for (int i=1; i<count; i=i+1)
    pos[i] = -1;
  pos[0] = 0;

  for (int i=1; !feof(fp); i=i+1) {
    char c = fgetc(fp);
    if (c=='\n') {
      for (int j=count-1; 0<j; j=j-1)
        pos[j] = pos[j-1];
      pos[0] = i;
    }
  }

  while (count>0) {
    count = count - 1;
    if (pos[count]>=0) break;
  }

  if (pos[count]!=-1) {
    fseek(fp, 0, pos[count]);
    while (!feof(fp)) {
      putchar(fgetc(fp));
    }
  }

  fclose(fp);
  return 0;
}
